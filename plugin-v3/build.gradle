plugins {
    id 'java-library'
    id 'p4ic.compat'
    id 'org.cyclonedx.bom'
}
version '1.6'

ideaImplementation.uses(
        'core',
        'core-ui',
        'vcs-core',
        'vcs',
        'ide',
        'util',
        'util-ui',
        'util-rt',
        'editor-ui-api',
        'extensions',
        'guiforms-rt',
        'concurrency',

        // Look at getting rid of the "impl" dependencies
        'vcs-impl',
        'ide-impl',

        'annotations',
        'jdom',
        'jgoodies-forms',
)

ideaTest.uses(
)

configurations {
    zip
}

repositories {
    flatDir { dirs '../lib/main', '../lib/test' }
}

// Required in order to have the source set build output be discovered.
project.evaluationDependsOn(':idea-p4server:api')

dependencies {
    //compatApi ()

    compatImplementation (
            project(':idea-p4server:api'),
            project(':idea-p4server:impl'),
            project(':p4java'),
            project(':swarm')
    )
    compatTest(
            'org.junit.jupiter:junit-jupiter-engine:5.8.2',
            'org.junit.platform:junit-platform-runner:1.8.2',
            'org.junit.platform:junit-platform-commons:1.8.2',
            'org.junit.platform:junit-platform-engine:1.8.2',
            'org.junit.platform:junit-platform-launcher:1.8.2',
            'org.junit.platform:junit-platform-suite-api:1.8.2',
            'org.junit.vintage:junit-vintage-engine:5.8.2',
            'org.hamcrest:hamcrest-core:2.2',
            'org.mockito:mockito-core:4.3.1',
            project(':p4d-test-core'),
            project(':idea-test-core'),
            project(path: ':idea-p4server:api', configuration: 'testArtifacts'),
            //project(':idea-p4server:api:test').sourceSets.test.output,
    )

    // Need to be really careful here.  This must only be
    // non-IDE jars required by the plugin and its dependent
    // libraries.
    zip (
            // swarm dependencies
            project(':swarm'),
            files(
                    tasks.getByPath(':p4java:jar').archivePath,
                    tasks.getByPath(':idea-p4server:api:jar').archivePath,
                    tasks.getByPath(':idea-p4server:impl:jar').archivePath,
                    tasks.getByPath(':swarm:jar').archivePath
            ),

            // Extra libraries from p4server impl
            'net.java.dev.jna:jna:5.10.0',
            'net.java.dev.jna:jna-platform:5.10.0',
    )
}


compileJava {
    options.deprecation = true
}


task packageVersion {
    dependsOn processResources
    inputs.file("src/main/resources/META-INF/plugin.xml")
    outputs.file("$buildDir/resources/main/p4ic-version.txt")
    outputs.file("$buildDir/resources/main/META-INF/plugin.xml")

    doFirst {
        def pluginFile = file("src/main/resources/META-INF/plugin.xml")
        def pluginXml = new XmlSlurper().parse(pluginFile)
        def version = pluginXml.version
        file("$buildDir/resources/main/p4ic-version.txt").text = version

        copy {
            from pluginFile
            into "$buildDir/resources/main/META-INF"
        }
    }
}
jar.dependsOn packageVersion
test.dependsOn packageVersion
compileTestJava.dependsOn packageVersion


// Without this explicit version set, the jar creates an archive
// with 1.0, whereas everything else is trying to pull in the
// top-level version identifier.
jar.archiveVersion = version

task showDeps {
    doFirst {
        configurations.all {
            logger.lifecycle("Configuration " + it)
            try {
                it.resolvedConfiguration.files.forEach { next ->
                    logger.lifecycle(" -> " + next.getAbsolutePath())
                }
            } catch (Exception e) {
                logger.lifecycle("  - could not resolve directly")
            }
        }
    }
}

cyclonedxBom {
    // skipConfigs is a list of configuration names to exclude when generating the BOM.
    // Because of the distribution of the plugin, we really only care about the
    // libraries included in the packaging, which are "invisible" to the end-user.
    skipConfigs += project.configurations.matching {
        return it.name != 'zip'
    }.getAsMap().keySet()
    // println("Skipped configurations: " + skipConfigs)
}

task assembleDepends {
    dependsOn ':p4java:jar'
    dependsOn ':idea-p4server:api:jar'
    dependsOn ':idea-p4server:impl:jar'
    dependsOn ':swarm:jar'
    dependsOn 'jar'
    dependsOn 'cyclonedxBom'
}


task assembleZip(type: Zip) {
    dependsOn 'assembleDepends'

    doFirst {
        copy {
            // For some reason, the jar file just isn't added in, even when
            // explicitly listed by name, or copied to another place.  It looks
            // like, here, the version is thought to be 1.6, whereas the jar
            // command sets the version to 1.0.
            // With the fix above that explicitly sets the jar version number,
            // this should be corrected now.
            // However, we want the correct jar file name, so that the JetBrains
            // plugin.xml finder is more accurate.
            from file(jar.archivePath)
            into "$buildDir/tmp"
            rename '.*', 'p4ic4idea.jar'
        }
    }

    into('lib') {
        from configurations.zip.resolvedConfiguration.files
        from configurations.zip.resolve()
        from file("$buildDir/tmp/p4ic4idea.jar")

        // All the licenses for the bundled jars...
        from "$rootDir/p4java/LICENSE-p4java.txt"
        from configurations.zip.resolvedConfiguration.files.collect { cfile ->
            def libname = cfile.name
            def p = libname.lastIndexOf('-')
            def i = 0
            while (p >= 0) {
                libname = libname.substring(0, p)
                File license = file("$rootDir/lib/main/LICENSE-" + libname + '.txt')
                i++
                if (license.exists()) {
                    return license
                }
                p = libname.lastIndexOf('-')
            }
            return null
        }.findAll {
            it != null
        }
    }

    into("/") {
        from "../README.md"
        from "../LICENSE"
        from "../CHANGES.md"
        from "$buildDir/reports/bom.xml"
        from "src/main/resources/META-INF/plugin.xml"
    }
}
assemble.dependsOn assembleZip

// Newer versions of IDEA seem to want the jar, not the zip.
task assembleUberJar(type: Zip) {
    dependsOn 'assembleDepends'

    archiveExtension = 'jar'
    classifier 'plugin'

    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.FAIL

    logger.info("Adding contents from " + jar.archivePath)
    into("/") {
        from "../README.md"
        from "../LICENSE"
        from "../CHANGES.md"
        from "$buildDir/reports/bom.xml"
        from "src/main/resources/META-INF/plugin.xml"
        from "$rootDir/p4java/LICENSE-p4java.txt"

        from zipTree(jar.archivePath)
        from configurations.zip.resolvedConfiguration.files.collect { cfile ->
            def libname = cfile.name
            def p = libname.lastIndexOf('-')
            def i = 0
            while (p >= 0) {
                libname = libname.substring(0, p)
                File license = file("$rootDir/lib/main/LICENSE-" + libname + '.txt')
                i++
                if (license.exists()) {
                    return license
                }
                p = libname.lastIndexOf('-')
            }
            if (['p4java-1.0.jar', 'api-1.0.jar', 'impl-1.0.jar', 'swarm-1.0.jar'].contains(cfile.name)) {
                return null
            }
            throw new Exception('Could not find license file for ' + libname + " (" + cfile.name + ')')
        }.findAll {
            it != null
        }
        from configurations.zip.resolvedConfiguration.files.collect { z ->
            logger.info("Adding contents from " + z)
            zipTree(z).matching { f -> 
                f.exclude('META-INF/MANIFEST.MF')
                f.exclude('META-INF/DEPENDENCIES')
                f.exclude('META-INF/NOTICE')
                f.exclude('META-INF/NOTICE.txt')
                f.exclude('META-INF/LICENSE')
                f.exclude('META-INF/LICENSE.txt')
                f.exclude('META-INF/AL2.0')
                f.exclude('META-INF/LGPL2.1')
            }
        }
    }
}
assemble.dependsOn assembleUberJar


task finalizeZip {
    dependsOn assembleZip
    dependsOn assembleUberJar

    doLast {
        copy {
            from assembleZip.outputs.files
            into "$rootDir"
            rename '.*', 'p4ic4idea.zip'
        }
        copy {
            from assembleUberJar.outputs.files
            into "$rootDir"
            rename '.*', 'p4ic4idea-plugin.jar'
        }
    }
}
assemble.dependsOn finalizeZip



group 'net.groboclown.p4ic4idea'
version '1.0'
