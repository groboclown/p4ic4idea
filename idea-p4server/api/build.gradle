plugins {
    id 'java-library'
    id 'p4ic.compat'
    id 'jacoco'
}


ideaApi.uses(
        'core',
        'vcs-core',
        'vcs',
        'ide',
        'analysis',
        'project-model',
        'util-io',
        'util-classloader',
        'util-strings',

        'annotations',
)
ideaImplementation.uses(
        // Look at how to pull in dependent libraries...
        'extensions',
        'util',
        'util-rt',
        'core-impl',

        'jdom',
        'fastutil',
        'trove4j',
        'intellij-util-collections',
        'kotlin-stdlib',
)
ideaTest.uses(
        'fastutil',
        'trove4j',
        'util-collections',
        'kotlin-stdlib',
)

dependencies {
    //compatApi ()
    compatImplementation (
            project(':p4java'),
            project(':swarm'),
            'org.apache.commons:commons-lang3:3.12.0'
    )
    compatTest(
            project(':idea-test-core'),

            // TODO pull in the test jars from the test-core as part of the build.
            'junit:junit:4.13.2',
            'org.junit.jupiter:junit-jupiter-engine:5.8.2',
            'org.junit.platform:junit-platform-runner:1.8.2',
            'org.junit.platform:junit-platform-commons:1.8.2',
            'org.junit.platform:junit-platform-engine:1.8.2',
            'org.junit.platform:junit-platform-launcher:1.8.2',
            'org.junit.platform:junit-platform-suite-api:1.8.2',
            'org.junit.vintage:junit-vintage-engine:5.8.2',
            'org.opentest4j:opentest4j:1.2.0',
            'org.apiguardian:apiguardian-api:1.1.2',
            'net.bytebuddy:byte-buddy:1.12.7',
            'org.objenesis:objenesis:3.2',
    )

    jacocoAnt (
            'org.jacoco:org.jacoco.core:0.8.7',
            'org.jacoco:org.jacoco.report:0.8.7',
            'org.jacoco:org.jacoco.agent:0.8.7',
            'org.jacoco:org.jacoco.ant:0.8.7',
            'org.ow2.asm:asm:9.2',
            'org.ow2.asm:asm-commons:9.2',
            'org.ow2.asm:asm-tree:9.2',
    )
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }

    // classDirectories = sourceSets.mainIdea173.output
}

compileJava {
    options.deprecation = true
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

project.tasks.check.dependsOn 'jacocoTestReport'

// allow the test artifacts to be used by other projects
configurations {
    testArtifacts.extendsFrom testCompile
}
task testArtifactsJar(type: Jar) {
    group 'build'

    from sourceSets.test.output
    destinationDirectory = file("$buildDir/libs")
    archiveClassifier = 'test'
}
artifacts {
    testArtifacts testArtifactsJar
}

repositories {
    flatDir { dirs "$rootDir/lib/main", "$rootDir/lib/test" }
}



group 'net.groboclown.p4ic4idea'
version '1.0'
