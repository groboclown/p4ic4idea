plugins {
    id 'java-library'
    id 'p4ic.compat'
}
version '1.6'

ideaImplementation.uses(
        'openapi',
        'core-api',
        'annotations-common',
        'annotations',
        'vcs-api-core',
        'vcs-api',
        'platform-api',
        'util',
        'util-rt',
        'editor-ui-api',
        'extensions',
        'jdom',
        'forms_rt',
        'jgoodies-forms',
        'projectModel-api',

        // Look at getting rid of the "impl" dependencies
        'platform-impl',
        'vcs-impl',
        'core-impl'
)

ideaTest.uses(
        'testFramework',
        'java-psi-impl',
        'java-runtime',
        'lang-api',
        'lang-impl',
        'trove4j'
)

configurations {
    zip
}

repositories {
    ivy {
        url "$rootDir/lib"
        layout "pattern", {
            artifact "main/[artifact]-[revision].[ext]"
            artifact "test/[artifact]-[revision].[ext]"
        }
    }
}

// Required in order to have the source set build output be discovered.
project.evaluationDependsOn(':idea-p4server:api')

dependencies {
    //compatApi ()

    compatImplementation (
            project(':idea-compat:compat-api'),
            project(':idea-p4server:api'),
            project(':idea-p4server:impl'),
            project(':p4java'),
            project(':swarm')
    )
    runtime (
            project(':idea-compat:v171')
    )
    compatTest(
            'org.junit.jupiter:junit-jupiter-engine:5.1.0',
            'org.junit.platform:junit-platform-runner:1.1.0',
            'org.junit.platform:junit-platform-commons:1.1.0',
            'org.junit.platform:junit-platform-engine:1.1.0',
            'org.junit.platform:junit-platform-launcher:1.1.0',
            'org.junit.platform:junit-platform-suite-api:1.1.0',
            'org.junit.vintage:junit-vintage-engine:5.1.0',
            'org.hamcrest:hamcrest-core:1.3',
            'org.mockito:mockito-core:2.0.86-beta',
            project(':p4d-test-core'),
            project(':idea-test-core'),
            project(path: ':idea-p4server:api', configuration: 'testArtifacts'),
            //project(':idea-p4server:api:test').sourceSets.test.output,
    )

    // Need to be really careful here.  This must only be
    // non-IDE jars required by the plugin and its dependent
    // libraries.
    zip (
            // swarm dependencies
            project(':swarm'),
            files(
                    tasks.getByPath(':idea-compat:compat-api:jar').archivePath,
                    tasks.getByPath(':p4java:jar').archivePath,
                    tasks.getByPath(':idea-compat:v171:jar').archivePath,
                    tasks.getByPath(':idea-p4server:api:jar').archivePath,
                    tasks.getByPath(':idea-p4server:impl:jar').archivePath,
                    tasks.getByPath(':swarm:jar').archivePath
            )
    )
}


task packageVersion {
    dependsOn processResources
    inputs.file("src/main/resources/META-INF/plugin.xml")
    outputs.file("$buildDir/resources/main/p4ic-version.txt")
    outputs.file("$buildDir/resources/main/META-INF/plugin.xml")

    doFirst {
        def pluginFile = file("src/main/resources/META-INF/plugin.xml")
        def pluginXml = new XmlSlurper().parse(pluginFile)
        def version = pluginXml.version
        file("$buildDir/resources/main/p4ic-version.txt").text = version

        copy {
            from pluginFile
            into "$buildDir/resources/main/META-INF"
        }
    }
}
jar.dependsOn packageVersion
// Without this explicit version set, the jar creates an archive
// with 1.0, whereas everything else is trying to pull in the
// top-level version identifier.
jar.version = version

task showDeps {
    doFirst {
        configurations.all {
            logger.lifecycle("Configuration " + it)
            try {
                it.resolvedConfiguration.files.forEach { next ->
                    logger.lifecycle(" -> " + next.getAbsolutePath())
                }
            } catch (Exception e) {
                logger.lifecycle("  - could not resolve directly")
            }
        }
    }
}

task assembleDepends {
    dependsOn ':p4java:jar'
    dependsOn ':idea-compat:compat-api:jar'
    dependsOn ':idea-compat:v171:jar'
    dependsOn ':idea-p4server:api:jar'
    dependsOn ':idea-p4server:impl:jar'
    dependsOn ':swarm:jar'
    dependsOn 'jar'
}


task assembleZip(type: Zip) {
    dependsOn 'assembleDepends'

    doFirst {
        copy {
            // For some reason, the jar file just isn't added in, even when
            // explicitly listed by name, or copied to another place.  It looks
            // like, here, the version is thought to be 1.6, whereas the jar
            // command sets the version to 1.0.
            // With the fix above that explicitly sets the jar version number,
            // this should be corrected now.
            // However, we want the correct jar file name, so that the JetBrains
            // plugin.xml finder is more accurate.
            from file(jar.archivePath)
            into "$buildDir/tmp"
            rename '.*', 'p4ic4idea.jar'
        }
    }

    into('lib') {
        from configurations.zip.resolvedConfiguration.files
        from configurations.zip.resolve()
        from file("$buildDir/tmp/p4ic4idea.jar")

        // All the licenses for the bundled jars...
        from "$rootDir/p4java/LICENSE-p4java.txt"
        from configurations.zip.resolvedConfiguration.files.collect { cfile ->
            def libname = cfile.name
            def p = libname.lastIndexOf('-')
            def i = 0
            while (p >= 0) {
                libname = libname.substring(0, p)
                File license = file("$rootDir/lib/main/LICENSE-" + libname + '.txt')
                i++
                if (license.exists()) {
                    return license
                }
                p = libname.lastIndexOf('-')
            }
            return null
        }.findAll {
            it != null
        }
    }

    into("/") {
        from "../README.md"
        from "../LICENSE"
        from "../CHANGES.md"
        from "src/main/resources/META-INF/plugin.xml"
    }
}
assemble.dependsOn assembleZip

// Newer versions of IDEA seem to want the jar, not the zip.
task assembleUberJar(type: Zip) {
    dependsOn 'assembleDepends'

    extension 'jar'
    classifier 'uber'

    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.FAIL

    logger.info("Adding contents from " + jar.archivePath)
    into("/") {
        from "../README.md"
        from "../LICENSE"
        from "../CHANGES.md"
        from "src/main/resources/META-INF/plugin.xml"
        from "$rootDir/p4java/LICENSE-p4java.txt"

        from zipTree(jar.archivePath)
        from configurations.zip.resolvedConfiguration.files.collect { cfile ->
            def libname = cfile.name
            def p = libname.lastIndexOf('-')
            def i = 0
            while (p >= 0) {
                libname = libname.substring(0, p)
                File license = file("$rootDir/lib/main/LICENSE-" + libname + '.txt')
                i++
                if (license.exists()) {
                    return license
                }
                p = libname.lastIndexOf('-')
            }
            return null
        }.findAll {
            it != null
        }
        from configurations.zip.resolvedConfiguration.files.collect { z ->
            logger.info("Adding contents from " + z)
            zipTree(z).matching { f ->
                f.exclude('META-INF/MANIFEST.MF')
                f.exclude('META-INF/DEPENDENCIES')
                f.exclude('META-INF/NOTICE')
                f.exclude('META-INF/NOTICE.txt')
                f.exclude('META-INF/LICENSE')
                f.exclude('META-INF/LICENSE.txt')
            }
        }
    }
}
assemble.dependsOn assembleUberJar


task finalizeZip {
    dependsOn assembleZip
    dependsOn assembleUberJar

    doLast {
        copy {
            from assembleZip.outputs.files
            into "$rootDir"
            rename '.*', 'p4ic4idea.zip'
        }
        copy {
            from assembleUberJar.outputs.files
            into "$rootDir"
            rename '.*', 'p4ic4idea-uber.jar'
        }
    }
}
assemble.dependsOn finalizeZip



group 'net.groboclown.p4ic4idea'
version '1.0'
